plugins {
    id "io.freefair.lombok" version "8.1.0" apply false
}

ext {
    jupiterVersion = '5.8.1'
}

allprojects {

    apply plugin: 'java'
    apply plugin: 'io.freefair.lombok'

    repositories {
        mavenCentral()
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    }

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Xlint:all', '-Werror', '-Xlint:-serial', '-Xlint:-processing', '-Xlint:-path']
    }

    tasks.withType(Test) {
        useJUnitPlatform()
        testLogging {
            failFast = true
            events = ['passed', 'failed']
        }
    }

    task unitTests(type: Test) {
        useJUnitPlatform {
            includeTags 'Unit'
        }
    }

    task integrationTests(type: Test) {
        useJUnitPlatform {
            includeTags 'Integration'
        }
    }

}

configure(project(':logging')) {
    dependencies {
        implementation 'ch.qos.logback:logback-classic:1.4.11'
        implementation 'org.apache.logging.log4j:log4j-api:2.6.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.6.2'
        implementation 'com.datadoghq:datadog-api-client:2.18.0'
    }
}

configure(project(':testing')) {
    dependencies {
        testImplementation 'org.instancio:instancio-junit:3.0.0'
        testImplementation "org.junit.jupiter:junit-jupiter-params:$jupiterVersion"
    }
}